plugins {
    id 'com.android.application'
    id 'com.mikepenz.aboutlibraries.plugin'
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "se.arctosoft.vault"
        minSdk 28
        targetSdk 34
        versionCode 19
        versionName "1.5.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".dev"
        }
        applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "Vault_${versionCode}_${defaultConfig.versionName}_${variant.buildType.name}.apk"
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding true
    }
    namespace 'se.arctosoft.vault'
}

dependencies {
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.security:security-crypto:1.0.0'
    implementation 'androidx.media3:media3-exoplayer:1.2.0'
    implementation 'androidx.media3:media3-ui:1.2.0'

    implementation 'com.github.bumptech.glide:glide:4.15.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.15.1'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'
    implementation 'com.mikepenz:aboutlibraries-core-android:10.8.3'
    implementation('com.mikepenz:aboutlibraries:10.8.3') {
        exclude group: 'com.mikepenz', module: 'aboutlibraries-core'
    }

    constraints { // remove when androidx.lifecycle:lifecycle-X-ktx works without it
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}

aboutLibraries {
    configPath = "config"
    // Remove the "generated" timestamp to allow for reproducible builds
    excludeFields = ["generated"]
}